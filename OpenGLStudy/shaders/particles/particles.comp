#version 440

layout (local_size_x = 512) in;

#include "../noise/hash.glsl"
#include "particles_common.glsl"

void main()
{
	uint particleIndex = gl_GlobalInvocationID.x;
	uint particleID = particleIndex + 1;

	vec3 velocity = sourceParticles[particleIndex].velocity;
	vec3 position = sourceParticles[particleIndex].position;

	if (resetParticles)
	{
		position = hash31(particleIndex) * vec3(simulationSize);
		velocity = vec3(0);
		destParticles[particleIndex].position = position;
		destParticles[particleIndex].velocity = velocity;
		return;
	}

	//if (isSorted(particle.position, particleID) < 0)
	//{
	//	destParticles[particleIndex].velocity = velocity;
	//	destParticles[particleIndex].
	//	return;
	//}
		

	float dt = 1.0 / 60.0 * timestep;

	ivec3 particleCell = ivec3(position);

	vec3 externalForce = vec3(0);
	
	for (int i = -1; i < 2; i++)
	for (int j = -1; j < 2; j++)
	for (int k = -1; k < 2; k++)
	for (int p = 0; p < PARTICLES_PER_CELL; p++)
	{
		ivec3 storedParticleCell = (particleCell + ivec3(i, j, k)) * ivec3(8, 1, 1) + ivec3(p, 0, 0);
		uint storedParticleID = texelFetch(particleMapSampler, storedParticleCell, 0).x;
		if (storedParticleID == NULL_ID)
			break;
		if (storedParticleID == particleID)
			continue;

		vec3 storedParticlePos = sourceParticles[storedParticleID - 1].position;
		vec3 delta = position - storedParticlePos;

		float deltaLength = length(delta);

		float repulsion = 50.0 / deltaLength;
		externalForce += delta / deltaLength * repulsion;
	}

	velocity += externalForce * dt;

	// Square magnitude drag
	velocity -= velocity * length(velocity) * dt * 0.1;

	// Gravity
	velocity += vec3(0, -10.0, 0) * dt;

	// Apply velocity
	position += velocity * dt;

	// Boundary condition
	vec3 boundary = vec3(simulationSize - 1);
	for (int i = 0; i < 3; i++)
	{
		if (position[i] < 0.0 || position[i] > boundary[i])
		{
			position[i] = clamp(position[i], 0.0, boundary[i]);
			velocity[i] *= -1.0;
		}
	}

	destParticles[particleIndex].position = position;
	destParticles[particleIndex].velocity = velocity;
}